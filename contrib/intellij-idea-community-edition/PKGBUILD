# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Jeremy Asuncion <jeremyasuncion808@gmail.com>

pkgname=intellij-idea-community-edition
pkgver=2017.1.5
_pkgver=171.4694.70
pkgrel=2
epoch=3
pkgdesc="IDE for Java, Groovy and other programming languages with advanced refactoring features"
arch=('any')
url="https://www.jetbrains.com/idea/"
license=('Apache')
backup=('usr/share/intellijidea-ce/bin/idea.vmoptions'
        'usr/share/intellijidea-ce/bin/idea64.vmoptions')
depends=('java-environment' 'giflib' 'libxtst' 'libxft' 'ttf-font'
         'coreutils' 'grep' 'which')
conflicts=('intellij-idea-libs')
replaces=('intellij-idea-libs')
install=idea.install
source=("http://download.jetbrains.com/idea/ideaIC-$pkgver-no-jdk.tar.gz"
        idea.desktop)
sha512sums=('d0132af742c1edb2d7d799385d26739e2a411b49e50d7409c282b919cc6fa25a08322b1880b16ae86e44780710a3054122e05de72525e7b7782cad46674fbb10'
            '0dae33ea761ba32b911103b7b54462deeae7b3ac54b95c3b177ec3386451269ba2f4bcf92329e13edfa1ce674c68003453591d06e8eab9443a39b1791e9b2444')

package() {
  install -d -m755 "$pkgdir/"usr/share
  cp -a "idea-IC-$_pkgver" "$pkgdir"/usr/share/intellijidea-ce

  # make sure that all files are owned by root
  chown -R root:root "$pkgdir/usr/share"

  # never wait on user input when starting idea
  sed -i '/.*read IGNORE.*/ d' "$pkgdir"/usr/share/intellijidea-ce/bin/idea.sh

  install -d -m755 "$pkgdir"/usr/bin
  ln -s /usr/share/intellijidea-ce/bin/idea.sh "$pkgdir"/usr/bin/idea.sh
  install -D -m644 "$srcdir"/idea.desktop "$pkgdir"/usr/share/applications/idea.desktop
  install -D -m644 "$pkgdir"/usr/share/intellijidea-ce/bin/idea.png \
                   "$pkgdir"/usr/share/pixmaps/idea.png

  # workaround FS#40934
  sed -i 's|lcd|on|'  "$pkgdir"/usr/share/intellijidea-ce/bin/*.vmoptions
}

# vim:set ts=2 sw=2 et:
