# $Id: PKGBUILD 228202 2017-05-16 10:24:38Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy Asuncion <jeremyasuncion808@gmail.com>

pkgname=npm
pkgver=5.1.0
pkgrel=1
pkgdesc='A package manager for javascript'
arch=('any')
url='https://www.npmjs.com/'
license=('custom:Artistic')
depends=('nodejs' 'semver')
provides=('nodejs-node-gyp')
makedepends=('procps-ng' 'marked-man')
optdepends=('python2: for node-gyp')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/npm/npm/archive/v$pkgver.tar.gz")
sha512sums=('c0412646dd09ed75932d3c530a0ba22ad5b8162449aa5c5dcd7882063480970d9473276b841f24e6918e2e47912f3e4542c08a8dc8e342cf45bc21378496885d')

prepare() {
  cd npm-$pkgver
  ln -sf /usr/bin/marked{,-man} node_modules/.bin/
}

build() {
  cd npm-$pkgver
  make
}

package() {
  local npm="$srcdir/npm-$pkgver/bin/npm-cli.js"

  mkdir -p $pkgdir/usr/src
  cp -r npm-$pkgver $pkgdir/usr/src/npm

  cd $pkgdir/usr/src/npm
  rm -f node_modules/.bin/marked{,-man}
  node $npm install -g --prefix=$pkgdir/usr

  # Why 777? :/
  chmod -R u=rwX,go=rX $pkgdir

  # Provide node-gyp executable
  cp bin/node-gyp-bin/node-gyp $pkgdir/usr/bin/node-gyp
  sed -i 's|"`dirname "$0"`/../../|"`dirname "$0"`/../lib/node_modules/npm/|' "$pkgdir"/usr/bin/node-gyp

  # Experimental dedup
  cd node_modules
  for dep in semver; do
    rm -r $dep
    node $npm link $dep
  done

  install -Dm644 "$srcdir"/npm-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
